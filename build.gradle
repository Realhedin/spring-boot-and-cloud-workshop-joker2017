buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.15.RELEASE'
  }
}

group = 'spring.cloud.workshop'
version = '0.0.1'


allprojects {
  apply plugin: 'io.spring.dependency-management'

  repositories {
    jcenter()
  }

  dependencyManagement {
    imports {
      mavenBom 'org.springframework.cloud:spring-cloud-starter-parent:Dalston.RELEASE'
    }

    dependencies {
      dependency 'org.projectlombok:lombok:1.18.2'
    }
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'org.springframework.boot'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  jar {
    archiveName = project.name + '.jar' //jar file without version
  }

  dependencies {
    compile 'org.springframework.cloud:spring-cloud-starter-eureka'
    compile 'com.fasterxml.jackson.module:jackson-module-parameter-names'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    compileOnly 'org.projectlombok:lombok'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
  }

  task dockerBuild {
    doLast {
      exec {
        commandLine 'docker', 'build', '--force-rm=true', "--tag=${project.name}", '.'
      }
    }
  }

  dockerBuild.dependsOn build

  springBoot {
    executable = true
  }
}

