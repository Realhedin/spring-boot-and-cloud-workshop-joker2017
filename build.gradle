buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE'
  }
}

group = 'spring.cloud.workshop'
version = '0.0.1'


allprojects {
  apply plugin: 'io.spring.dependency-management'

  repositories {
    jcenter()
  }

  dependencyManagement {
    imports {
      mavenBom 'org.springframework.cloud:spring-cloud-starter-parent:Dalston.RELEASE'
      mavenBom 'org.springframework.cloud:spring-cloud-bus:1.3.1.RELEASE'
    }

    dependencies {
      dependency 'org.projectlombok:lombok:1.16.8'
    }
  }
}

subprojects { p ->
  apply plugin: 'java'
  apply plugin: 'org.springframework.boot'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  jar {
    archiveName = project.name + '.jar' //jar file without version
  }

  dependencies {
    if (p.name.endsWith('-service')) {
      compile 'org.springframework.cloud:spring-cloud-starter-eureka'
      compile 'org.springframework.boot:spring-boot-starter-actuator'
      compile 'com.fasterxml.jackson.module:jackson-module-parameter-names'
      compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
      compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
      compile 'org.springframework.cloud:spring-cloud-config-client'
      compile 'org.springframework.boot:spring-boot-starter-actuator'

      testCompile 'org.springframework.boot:spring-boot-starter-test'
      compileOnly 'org.springframework.boot:spring-boot-configuration-processor'
      compile 'org.springframework.cloud:spring-cloud-starter-bus-amqp'

      compile 'org.springframework.cloud:spring-cloud-starter-hystrix'

      compile 'org.springframework.cloud:spring-cloud-netflix-hystrix-stream'
      compile 'org.springframework.cloud:spring-cloud-starter-stream-rabbit'

      compile 'org.springframework.cloud:spring-cloud-starter-sleuth'
      compile 'org.springframework.cloud:spring-cloud-starter-zipkin'
      compile 'org.springframework.cloud:spring-cloud-sleuth-zipkin'
    }


    compileOnly 'org.projectlombok:lombok'
  }

  task dockerBuild {
    doLast {
      exec {
        commandLine 'docker', 'build', '--force-rm=true', "--tag=${project.name}", '.'
      }
    }
  }

  dockerBuild.dependsOn build

  springBoot {
    executable = true
  }
}

